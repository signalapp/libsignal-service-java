apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

archivesBaseName = "signal-service-android"
version          = lib_signal_service_version_number
group            = lib_signal_service_group_info

repositories {
    google()
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation libs.libsignal.android
    api (project(':libsignal-service')) {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
}

android {
    namespace 'org.whispersystems.signalservice'
    compileSdkVersion 32
    buildToolsVersion '31.0.0'

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 31
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

tasks.whenTaskAdded { task ->
    if (task.name.equals("lint")) {
        task.enabled = false
    }
}

def isReleaseBuild() {
    return version.contains("SNAPSHOT") == false
}

def getReleaseRepositoryUrl() {
    return hasProperty('sonatypeRepo') ? sonatypeRepo
            : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
}

def getRepositoryUsername() {
    return hasProperty('whisperSonatypeUsername') ? whisperSonatypeUsername : ""
}

def getRepositoryPassword() {
    return hasProperty('whisperSonatypePassword') ? whisperSonatypePassword : ""
}

afterEvaluate {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = 'signal-service-android'
                from components.release

                pom {
                    name = 'signal-service-android'
                    packaging = 'aar'
                    description = 'Signal Service communication library for Android'
                    url = 'https://github.com/WhisperSystems/libsignal-service-java'

                    scm {
                        url = 'scm:git@github.com:WhisperSystems/libsignal-service-java.git'
                        connection = 'scm:git@github.com:WhisperSystems/libsignal-service-java.git'
                        developerConnection = 'scm:git@github.com:WhisperSystems/libsignal-service-java.git'
                    }

                    licenses {
                        license {
                            name = 'GPLv3'
                            url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                            distribution = 'repo'
                        }
                    }

                    developers {
                        developer {
                            name = 'Moxie Marlinspike'
                        }
                    }
                }

            }
        }

        repositories {
            maven {
                url = getReleaseRepositoryUrl()
                credentials {
                    username getRepositoryUsername()
                    password getRepositoryPassword()
                }
            }
        }
    }

    signing {
        required { isReleaseBuild() && gradle.taskGraph.hasTask("uploadArchives") }
        sign publishing.publications.mavenJava
    }

    task installArchives(type: Upload) {
        description "Installs the artifacts to the local Maven repository."
        configuration = configurations['archives']
        repositories {
            mavenLocal()
        }
    }
}
